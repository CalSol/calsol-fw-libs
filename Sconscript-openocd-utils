# Adds a flash target that invokes OpenOCD
#
# These environment variables must be defined at the time in invocation:
# - FLASHINTERFACE: OpenOCD interface .cfg
# - FLASHTARGETCFG: OpenOCD target .cfg
#
# Usage:
#   SConscript('calsol-fw-libs/SConscript-openocd-utils', exports='env')
#   env.Append(FLASHINTERFACE='interface/cmsis-dap.cfg')
#   env.Append(FLASHTARGETCFG='lpc1549_openocd.cfg')
#   env.Flash('flash', myProgram)

Import('env')

env['FLASHCOM'] = """
  openocd
  -f $FLASHINTERFACE
  -f $FLASHTARGETCFG
  -c "program ${str(SOURCE).replace('\\\\', '/')}"
  -c "reset run"
  -c "exit"
""".replace('\n', ' ')

env.Append(BUILDERS = {'Flash': Builder(
  action=Action('$FLASHCOM', '$FLASHCOMSTR'),
  src_suffix='.elf')
})
