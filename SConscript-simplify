# Optionally simplifies the SCons build console outs, using an abbreviation
# plus the output file, instead of displaying the full command.
#
# Usage:
#   SConscript('calsol-fw-libs/SConscript-simplify', exports='env')

Import('env')

def simplify_output(env, mappings):
  pad_len = max([len(val) for val in mappings.values()]) + 2
  for key, val in mappings.items():
    env[key] = val + (' ' * (pad_len - len(val))) + '$TARGET'

AddOption(
  '--verbose',
  action='store_true',
  help='Print full command being executed, as opposed to summary and target.'
)
if not GetOption('verbose'):
  simplify_output(env, {
    'ASPPCOMSTR': 'AS',
    'ASCOMSTR': 'AS',
    'ARCOMSTR': 'AR',
    'CCCOMSTR': 'CC',
    'CXXCOMSTR': 'CXX',
    'LINKCOMSTR': 'LD',
    'RANLIBCOMSTR': 'RANLIB',
  })

  # TODO: integrate this with everything else
  env['FLASHCOMSTR'] = "FLASH  $SOURCE  ($FLASHTARGETCFG)"
